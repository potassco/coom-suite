product {
	        Color   color
    0..1    Basket  basket
            Saddle  saddle
	        Wheel   frontWheel
	        Wheel   rearWheel
            Carrier carrier
}

structure Basket {
    Position    position
    Color       color
}

structure Carrier {
    0..2 Bag bag
}

structure Bag {
	Capacity capacity
	Material material
}

enumeration Color {
	Silver
	White
	Black
	Blue
}

enumeration Position {
    Front
    Back
}

enumeration Saddle {
    Standard
    Comfort
    Vintage
}

enumeration Wheel {
	attribute num size

	W26	= (	 26	)
	W27	= (	 27	)
	W28	= (	 28	)
	W29	= (	 29	)
}

enumeration Capacity {
	attribute num volume

	B10  = ( 10 )
	B20  = ( 20 )
	B50  = ( 50 )
	B100 = (100 )
}

enumeration Material {
	Cotton
	Leather
	Polyester
}

behavior {
    require color = basket.color

    condition basket.position = Front
    require frontWheel.size < 29

    condition carrier.bag.capacity.volume = 100
    require rearWheel.size >= 28

    condition saddle = Vintage
    require carrier.bag.material = Leather

	require frontWheel.size = rearWheel.size
}

behavior Bag {
    combinations  (material     capacity)
	allow         (Leather      B10             )
    allow         (Cotton       (B20, B50)      )
	allow         (Polyester    (B20, B50, B100))
}
