product {
            num /l  0-200   totalVolume
            num /l  0-200   requestedVolume
    2..*    Bag             bags
            Color           color
}

structure Bag {
         Color  color
         Size   size
    0..* Pocket pockets
}

enumeration Pocket { S M L XL }

enumeration Color { Green Blue Red }

enumeration Size {
    attribute num weight
    attribute num volume

    small   = ( 10 12 )
    medium  = ( 15 16 )
    large   = ( 25 20 )
}

behavior {
    explanation "The total volume is equal to the sum of the volumes of all bags."
    require totalVolume = sum(bags.size.volume)

    explanation "The total volume must be greater than or equal to the requested volume."
    require totalVolume >= requestedVolume
}

behavior {
    require color = Blue
}

behavior {
    condition color = Blue
    require 3 <= count(bags)
}

behavior Bag {
    require color = Green
}

behavior Bag {
    require 2 <= count(pockets)
}

behavior Bag {
    explanation "The size of the pockets of a bag have to match the size of the bag."
    combinations (size     pockets)
    allow        (small    S      )
    allow        (medium   M      )
    allow        (large    (L,XL) )
}
